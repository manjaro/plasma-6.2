# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=xdg-desktop-portal-kde
pkgver=6.2.4
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal using Qt/KF5'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         kcrash
         kglobalaccel
         kiconthemes
         ki18n
         kio
         kio-fuse
         kirigami
         kitemviews
         knotifications
         kpipewire
         kservice
         kstatusnotifieritem
         kwayland
         kwindowsystem
         libxkbcommon
         plasma-workspace
         qt6-base
         qt6-declarative
         qt6-wayland
         wayland
         xdg-desktop-portal)
makedepends=(extra-cmake-modules
             plasma-wayland-protocols
             wayland-protocols)
provides=(xdg-desktop-portal-impl)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig}
        steamos-auto-accept-remote.patch)
sha256sums=('daf2d8769d0f45264ef1f48b63f5274446222574ff832e40f21d06a4c3822d1d'
            'SKIP'
            'ae83211bd2f941cbb9947b01944d3a3927c7248e083a8dc32dd3ad767b30bf11')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  cd $pkgname-$pkgver
  # auto-allow remote control without prompt -- needed for steamdeck controls
  patch -p1 -i ../steamos-auto-accept-remote.patch
}

build() {
  cmake -B build  -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
